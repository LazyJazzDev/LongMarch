
set(LONG_MARCH_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR} CACHE STRING "Grassland Source Dir")
set(LONG_MARCH_INCLUDE_DIR ${LONG_MARCH_SOURCE_DIR}/src CACHE STRING "Grassland Include Dir")
set(LONG_MARCH_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR} CACHE STRING "Grassland Binary Dir")

function(XXD input_file output_file dir_name)
    # Add command with relative path
    add_custom_command(
            OUTPUT ${output_file}
            COMMAND ${LONG_MARCH_BINARY_DIR}/scripts/simple_xxd ${input_file} ${output_file}
            COMMAND ${CMAKE_COMMAND} -E echo "Generating ${output_file} from ${input_file}"
            WORKING_DIRECTORY ${dir_name}
            DEPENDS simple_xxd ${input_file}
    )
endfunction()

function(PACK_SHADER_CODE TARGET_NAME)
    message(STATUS "PACK_SHADER_CODE ${CMAKE_CURRENT_SOURCE_DIR}")

    # Find all the shader files under current directory
    file(GLOB_RECURSE SHADER_FILES
            ${CMAKE_CURRENT_SOURCE_DIR}/*.glsl
            ${CMAKE_CURRENT_SOURCE_DIR}/*.vert
            ${CMAKE_CURRENT_SOURCE_DIR}/*.frag
            ${CMAKE_CURRENT_SOURCE_DIR}/*.tesc
            ${CMAKE_CURRENT_SOURCE_DIR}/*.tese
            ${CMAKE_CURRENT_SOURCE_DIR}/*.comp
            ${CMAKE_CURRENT_SOURCE_DIR}/*.geom
            ${CMAKE_CURRENT_SOURCE_DIR}/*.rgen
            ${CMAKE_CURRENT_SOURCE_DIR}/*.rchit
            ${CMAKE_CURRENT_SOURCE_DIR}/*.rmiss
            ${CMAKE_CURRENT_SOURCE_DIR}/*.hlsl
            ${CMAKE_CURRENT_SOURCE_DIR}/*.hlsli
            RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}
            LIST_DIRECTORIES false
    )

    # Extract shader files the relative path from the current source directory
    foreach (SHADER_FILE ${SHADER_FILES})
        file(RELATIVE_PATH RELATIVE_SHADER_FILE ${CMAKE_CURRENT_SOURCE_DIR} ${SHADER_FILE})
        list(APPEND RELATIVE_SHADER_FILES ${RELATIVE_SHADER_FILE})
    endforeach ()

    set(SHADER_FILES ${RELATIVE_SHADER_FILES})

    # Show all SHADER_FILES
    foreach (SHADER_FILE ${SHADER_FILES})
        message(STATUS "SHADER_FILE ${SHADER_FILE}")
    endforeach ()

    # Use the XXD cmake function generate header files in corresponding build directory
    foreach (SHADER_FILE ${SHADER_FILES})
        set(HEADER_FILE ${CMAKE_CURRENT_BINARY_DIR}/${SHADER_FILE}.h)
        XXD(${SHADER_FILE} ${HEADER_FILE} ${CMAKE_CURRENT_SOURCE_DIR})
        list(APPEND HEADER_FILES ${HEADER_FILE})
    endforeach ()

    # Add the generated header files to the target
    target_sources(${TARGET_NAME} PRIVATE ${HEADER_FILES})

    # The custom command should be executed before the target is built
    add_custom_target(
            ${TARGET_NAME}_shader_files ALL
            DEPENDS ${HEADER_FILES}
    )

    add_dependencies(${TARGET_NAME} ${TARGET_NAME}_shader_files)

    # Output the generated header files to built_in_shaders.inl in form #include<PATH_HEADER>

    # Get the relative path of the generated header files
    foreach (HEADER_FILE ${HEADER_FILES})
        file(RELATIVE_PATH RELATIVE_HEADER_FILE ${CMAKE_CURRENT_BINARY_DIR} ${HEADER_FILE})
        list(APPEND RELATIVE_HEADER_FILES ${RELATIVE_HEADER_FILE})
    endforeach ()

    # Generate the built_in_shaders.inl
    set(BUILT_IN_SHADERS_INL ${CMAKE_CURRENT_BINARY_DIR}/built_in_shaders.inl)
    file(WRITE ${BUILT_IN_SHADERS_INL} "// This file is generated by CMake\n")
    foreach (RELATIVE_HEADER_FILE ${RELATIVE_HEADER_FILES})
        file(APPEND ${BUILT_IN_SHADERS_INL} "#include \"${RELATIVE_HEADER_FILE}\"\n")
    endforeach ()

    # List all the shader info in a map std::map<std::string, std::pair<const char *, unsigned int>>, generate a global variable
    file(APPEND ${BUILT_IN_SHADERS_INL} "\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "std::map<std::string, std::pair<const char *, unsigned int>> shader_list = {\n")
    foreach (SHADER_FILE ${SHADER_FILES})
        string(REPLACE "." "_" VAR_NAME ${SHADER_FILE})
        string(REPLACE "/" "_" VAR_NAME ${VAR_NAME})
        string(REPLACE "\\" "_" VAR_NAME ${VAR_NAME})
        # Reintercast the variable name to const char *
        file(APPEND ${BUILT_IN_SHADERS_INL} "    {\"${SHADER_FILE}\", {reinterpret_cast<const char *>(${VAR_NAME}), ${VAR_NAME}_len}},\n")
    endforeach ()
    file(APPEND ${BUILT_IN_SHADERS_INL} "};\n")


    # Generate a function GetShaderCode in built_in_shaders.inl, input is the file name, output is the shader code, both are string
    file(APPEND ${BUILT_IN_SHADERS_INL} "\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "std::string GetShaderCode(const std::string& file_name) {\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "    return std::string(shader_list[file_name].first, shader_list[file_name].second);\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "}\n")

    file(APPEND ${BUILT_IN_SHADERS_INL} "\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "::grassland::VirtualFileSystem GetShaderVirtualFileSystem() {\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "    ::grassland::VirtualFileSystem vfs;\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "    for (const auto& [filename, data] : shader_list) {\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "        vfs.WriteFile(filename, data.first, data.second);\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "    }\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "    return vfs;\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "}\n")
    file(APPEND ${BUILT_IN_SHADERS_INL} "\n")

    target_sources(${TARGET_NAME} PRIVATE ${BUILT_IN_SHADERS_INL})
    target_include_directories(${TARGET_NAME} PRIVATE ${CMAKE_CURRENT_BINARY_DIR})
    message(STATUS "Including ${CMAKE_CURRENT_BINARY_DIR} in ${TARGET_NAME}")

endfunction()
